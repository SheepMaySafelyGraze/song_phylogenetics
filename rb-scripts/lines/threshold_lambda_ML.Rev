# read data

motifs <- readDiscreteCharacterData("data/motif_data_tmp.nex")
phylogeny <- readTrees("data/tree_tmp.tree")[1]
M := phylogeny.taxa().size()
moves = VectorMoves()
monitors = VectorMonitors()

### setup threshold model

threshold ~ dnNormal(mean=0.0, sd=10.0)
moves.append(mvSlide(threshold, weight=10.0))

liability[M-1] := 0

lambda ~ dnExponential(1.0)
moves.append(mvSlide(lambda, weight=10.0))

# interior liabilities
for (i in (M-2):1) {
    branch[i+M] := phylogeny.branchLength(i+M)
    liability[i] ~ dnNormal(mean = liability[phylogeny.parent(i+M) - M], sd = sqrt(branch[i+M]))
}

# bernoulli variables for above/below threshold at tips
for (i in 1:M) {
    branch[i] := phylogeny.branchLength(i)
    t[i] := phylogeny.rootAge() - phylogeny.nodeAge(i)
    mu[i] := liability[phylogeny.parent(i) - M]

    p[i] := 1 - pnorm((threshold - lambda*mu[i]) / sqrt(abs(lambda*branch[i] + (1-lambda)*t[i])), mean=0, sd=1.0)
    motif_present[i] ~ dnBernoulli(Probability(p[i]))
}


for (i in 1:(M-2)) {
    moves.append(mvSlide(liability[i]))
}


for (i in 1:M){
    motif_present[i].clamp(motifs[M-i + 1][1])
}



### monitors

monitors.append(mnScreen(threshold, printgen=n_generations-1))
monitors.append(mnFile(threshold, filename= save_loc + "/threshold.log", printgen=printgen))
monitors.append(mnFile(liability, filename= save_loc + "/interior_liabilities.log", printgen=printgen))

model_ = model(phylogeny)
file = save_loc + "/ppos/model.out"
pow_p = powerPosterior(model_, moves, monitors, file, cats=50)
pow_p.burnin(generations=burnin_generations, tuningInterval=tuning_interval)
pow_p.run(generations=n_generations/50)
ss = steppingStoneSampler(file=file, powerColumnName="power", likelihoodColumnName="likelihood")
ml := ss.marginal()
write(ml, filename= save_loc + "/ml_res.file")


